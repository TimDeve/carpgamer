(system-include "Arduino.h")

(load "git@github.com:TimDeve/pair-spread@v0.1.0")

(load "config.carp")

(load "src/lib/serial.carp")
(load "src/lib/arcada.carp")
(load "src/lib/sys.carp")
(load "src/lib/analog.carp")
(load "src/models.carp")
(load "src/collisions.carp")
(load "src/colors.carp")
(load "src/patterns.carp")

(use Array)

(def debug false)

(def screen-width 160)
(def screen-height 128)

(sig gen-random-seed (Fn [] Double))
(defn gen-random-seed []
  (Double.from-long (+ (Sys.micros) (Long.from-int (Analog.read 3)))))

(sig init-world (Fn [] World))
(defn init-world []
  (World.init
    (WorldState.Running)
    (Player.init (/ screen-width 2) (/ screen-height 2))
    (Pair.spread [_offset blocks (gen-patterns 160)]
      blocks)))

(sig init-intro-world (Fn [] World))
(defn init-intro-world []
  (World.set-state (init-world) (WorldState.Intro)))

(sig clean-up-joystick-input (Fn [Int] Int))
(defn clean-up-joystick-input [input] (/ input 30))

(sig draw-background-circle (Fn [] ()))
(defn draw-background-circle []
  (Arcada.Canvas.draw-filled-circ
    (/ screen-width 2)
    (/ screen-height 2)
    22
    Colors.dark-grey))

(sig draw-player (Fn [(Ref Player)] ()))
(defn draw-player [player]
  (Arcada.Canvas.draw-filled-circ
    @(Player.x-pos player)
    @(Player.y-pos player)
    player-radius
    Colors.white))

(sig draw-blocks (Fn [(Ref (Array Block))] ()))
(defn draw-blocks [blocks]
  (foreach [block blocks]
    (Arcada.Canvas.draw-filled-rect
      @(Block.x-pos block)
      @(Block.y-pos block)
      @(Block.w block)
      @(Block.h block)
      Colors.white)))

(sig maybe-draw-death-screen (Fn [WorldState] ()))
(defn maybe-draw-death-screen [world-state]
  (match world-state
    WorldState.Died (Arcada.Canvas.draw-filled-rect 0 0 100 10 Colors.light-red)
    _               ()))

(sig maybe-draw-start-screen (Fn [WorldState] ()))
(defn maybe-draw-start-screen [world-state]
  (match world-state
    WorldState.Intro (Arcada.Canvas.draw-filled-rect 0 0 100 10 Colors.light-blue)
    _                ()))

;(sig maybe-draw-debug (Fn [(Ref Player) (Ref (Array Block))] ()))
(defn maybe-draw-debug [player blocks]
  (if debug
    (with Arcada.Canvas
      (do
        (draw-text 0 0 1 &(String.concat &[@"Player:"
                                           @" "
                                           (str (Player.x-pos player))
                                           @" "
                                           (str (Player.y-pos player))]))
        (draw-text 0 10 1 &(String.concat
                              &(Array.copy-map
                                 &(fn [block]
                                      (String.concat
                                        &[(str (Block.x-pos block))
                                          @" "
                                          (str (Block.y-pos block))
                                          @"\n"]))
                                 blocks)))))
    ()))

(sig init-display (Fn [] ()))
(defn init-display []
  (do
    (Arcada.init-display)
    (Arcada.fill-screen Colors.darker-grey)
    (Sys.delay 100) ; Wait a little bit so the display doesn't flash white
    (Arcada.set-backlight 255)))

; Most of this code is temporary, blocks will be randomly generated
(sig update-block (Fn [(Ref Block)] Block))
(defn update-block [block]
  (match @(Block.direction block)
    Direction.Up    (Block.update-y-pos @block &Int.dec)
    Direction.Right (Block.update-x-pos @block &Int.inc)
    Direction.Down  (Block.update-y-pos @block &Int.inc)
    Direction.Left  (Block.update-x-pos @block &Int.dec)))

(sig update-positions (Fn [World] World))
(defn update-positions [world]
  (with Arcada
    (let [joy-x (read-joystick-x) joy-y (read-joystick-y)]
      (match @(World.state &world)
        WorldState.Running (World.init
                            @(World.state &world)
                            (Player.init
                              (+ (/ screen-width 2) (clean-up-joystick-input joy-x))
                              (+ (/ screen-height 2) (clean-up-joystick-input joy-y)))
                            (copy-map &update-block (World.blocks &world)))

        _                 world))))


(sig block-should-not-be-removed? (Fn [(Ref Block)] Bool))
(defn block-should-not-be-removed? [block]
  (match @(Block.direction block)
     Direction.Up    (> @(Block.y-pos block) (* (/ @(Block.h block) 2) -1))
     Direction.Right (< @(Block.x-pos block) (+ screen-width (/ @(Block.w block) 2)))
     Direction.Down  (< @(Block.y-pos block) (+ screen-height (/ @(Block.h block) 2)))
     Direction.Left  (> @(Block.x-pos block) (* (/ @(Block.w block) 2) -1))))

(sig remove-blocks (Fn [World] World))
(defn remove-blocks [world]
  (match @(World.state &world)
    WorldState.Running (World.set-blocks world
                                         (endo-filter
                                           &block-should-not-be-removed?
                                           @(World.blocks &world)))
    _                  world))

(sig check-collisions (Fn [World] World))
(defn check-collisions [world]
  (match @(World.state &world)
    WorldState.Running (let [colliding (player-colliding-with-blocks?
                                        (World.player &world)
                                        (World.blocks &world))]
                        (World.set-state
                         world
                         (if colliding
                             (WorldState.Died)
                             (WorldState.Running))))


    _                  world))

(sig check-for-pause (Fn [World] World))
(defn check-for-pause [world]
  (with Arcada
    (let [just-pressed-buttons (just-pressed-buttons)]
      (match @(World.state &world)
        WorldState.Running (World.set-state
                              world
                              (if (start-pressed? just-pressed-buttons)
                                  (WorldState.Paused)
                                  (WorldState.Running)))

        WorldState.Paused (if (start-pressed? just-pressed-buttons)
                              (World.set-state world (WorldState.Running))
                              world)

       _                  world))))

(sig check-for-game-start (Fn [World] World))
(defn check-for-game-start [world]
  (with Arcada
    (let [just-pressed-buttons (just-pressed-buttons)]
      (match @(World.state &world)
        WorldState.Died  (if (start-pressed? just-pressed-buttons)
                             (init-world)
                             world)

        WorldState.Intro (if (start-pressed? just-pressed-buttons)
                             (init-world)
                             world)

       _                 world))))

(sig update (Fn [World] World))
(defn update [world]
  ; read-buttons need to be read only once per tick or it'll break
  ; just-pressed-buttons and just-released-buttons
  (let [_buttons (Arcada.read-buttons)]
    (==> world
     (update-positions)
     (check-collisions)
     (remove-blocks)
     (check-for-pause)
     (check-for-game-start))))

(sig draw (Fn [(Ref World)] ()))
(defn draw [world]
  (with Arcada.Canvas
    (do
     (fill-screen Colors.darker-grey)
     (draw-background-circle)
     (draw-player (World.player world))
     (draw-blocks (World.blocks world))
     (maybe-draw-death-screen @(World.state world))
     (maybe-draw-start-screen @(World.state world))
     (maybe-draw-debug (World.player world) (World.blocks world))
     (blit 0 0))))

(sig game-loop (Fn [World] ()))
(defn game-loop [world]
  (forever-do
   (set! world (update world))
   (draw &world)))

(sig setup (Fn [] ()))
(defn setup []
  (do
   (System.carp-init-globals 0 0)
   (Arcada.init)
   (init-display)
   (Arcada.Canvas.init screen-width screen-height)
   (Random.seed-from (gen-random-seed))
   (game-loop (init-intro-world))))

; Don't need loop but it needs to be declared to make Arduino happy
(sig loop (Fn [] ()))
(defn loop [] ())

