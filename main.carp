(system-include "Arduino.h")

(load "config.carp")

(load "src/lib/serial.carp")
(load "src/lib/arcada.carp")
(load "src/lib/sys.carp")
(load "src/colors.carp")

(def screen-width 160)
(def screen-height 128)

(deftype Player [x-pos Int, y-pos Int])

(deftype World [player Player])

(sig init-world (Fn [] World))
(defn init-world [] (World.init (Player.init 0 0)))

(sig clean-up-joystick-input (Fn [Int] Int))
(defn clean-up-joystick-input [input] (/ input 30))

(sig draw-background-circle (Fn [] ()))
(defn draw-background-circle []
  (Arcada.Canvas.draw-filled-circ
    (/ screen-width 2)
    (/ screen-height 2)
    22
    Colors.dark-grey))

(sig draw-player (Fn [(Ref Player)] ()))
(defn draw-player [player]
  (Arcada.Canvas.draw-filled-circ
    (+ (/ screen-width 2) @(Player.x-pos player))
    (+ (/ screen-height 2) @(Player.y-pos player))
    5
    Colors.white))

(sig init-display (Fn [] ()))
(defn init-display []
  (do
    (Arcada.init-display)
    (Arcada.fill-screen Colors.darker-grey)
    (Sys.delay 100) ; Wait a little bit so the display doesn't flash white
    (Arcada.set-backlight 255)))

(sig update (Fn [World] World))
(defn update [world]
  (with Arcada
      (World.init
        (Player.init
         (clean-up-joystick-input (read-joystick-x))
         (clean-up-joystick-input (read-joystick-y))))))

(sig draw (Fn [(Ref World)] ()))
(defn draw [world]
  (with Arcada.Canvas
    (do
      (fill-screen Colors.darker-grey)
      (draw-background-circle)
      (draw-player (World.player world))
      (blit 0 0))))

(sig setup (Fn [] ()))
(defn setup []
  (do
   (System.carp-init-globals 0 0)
   (Arcada.init)
   (init-display)
   (Arcada.Canvas.init screen-width screen-height)
   (let [world (init-world)]
     (while true
      (do
        (set! world (update world))
        (draw &world))))))

; Don't need loop but it needs to be declared to make Arduino happy
(sig loop (Fn [] ()))
(defn loop [] ())

